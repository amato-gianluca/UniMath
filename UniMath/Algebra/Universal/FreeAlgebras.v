(** * Variables and free algebras. *)

(**
   This file contains a formalization of terms with variables and the free algebra generated by
   an infinite set of variables, together with the corresponding universal mapping property.
 *)

Require Import UniMath.Foundations.All.
Require Import UniMath.Combinatorics.Vectors.
Require Import UniMath.Algebra.Universal.Signatures.
Require Import UniMath.Algebra.Universal.Terms.
Require Import UniMath.Algebra.Universal.Algebras.

Local Open Scope hom.

Section Variables.

  Definition vsignature (sigma : signature) (V: hSet): signature
    := setcoprod (names sigma) V,,
                 sumofmaps (@arity sigma) (λ _, 0).

  Definition vterm (sigma: signature) (V: hSet) := term (vsignature sigma V).

  Context {sigma : signature}.

  Definition namelift {V: hSet} (nm: names sigma) : names (vsignature sigma V) := inl nm.

  Definition varname {V: hSet} (v: V): names (vsignature sigma V) := inr v.

  Definition var {V: hSet} (v: V): vterm sigma V := build_term_curried (varname v).

  Definition fromvterm {A:UU} {V: hSet}
             (op : (∏ (nm : names sigma), Vector A (arity nm) → A))
             (α : V → A)
    : vterm sigma V → A.
  Proof.
    refine (@fromterm (vsignature sigma V) A _).
    induction nm as [nm | nm].
    - exact (op nm).
    - exact (λ rec, α nm).
  Defined.

  Lemma fromvtermstep {A: UU} {V: hSet}
                      (nm: names sigma)
                      (op : (∏ (nm : names sigma), Vector A (arity nm) → A))
                      (α : V → A)
                      (v: Vector (term (vsignature sigma V)) (arity nm))
    : fromvterm op α (build_term (namelift nm) v) = op nm (vector_map (fromvterm op α) v).
  Proof.
    unfold fromvterm, fromterm.
    rewrite term_ind_step.
    rewrite vector_map_as_mk_vector.
    apply idpath.
  Defined.

End Variables.

Section FreeAlgebras.

  Definition free_algebra (sigma: signature) (V: hSet): algebra sigma :=
    make_algebra (termset (vsignature sigma V)) (λ nm: names sigma, build_term (namelift nm)).

  Context {sigma: signature}.

  Definition veval (a : algebra sigma) {V: hSet} (α: V → support a): free_algebra sigma V → support a
    := fromvterm (op a) α.

  Lemma vevalstep {a: algebra sigma} {V: hSet} (α: V → support a) (nm: names sigma) (v: Vector (vterm sigma V) (arity nm))
    : veval a α (build_term (namelift nm) v) = op a nm (vector_map (veval a α) v).
  Proof.
    unfold veval.
    apply fromvtermstep.
  Defined.

  Lemma ishomveval (a: algebra sigma) {V: hSet} (α: V → support a): ishom (veval a α).
  Proof.
    red.
    intros.
    apply vevalstep.
  Defined.

  Definition vevalhom (a: algebra sigma) {V: hSet} (α: V → support a): free_algebra sigma V ↦ a
    := make_hom (ishomveval a α).

  Definition universalmap {a: algebra sigma} {V: hSet} (α: V → support a)
    : ∑ h: free_algebra sigma V ↦ a, ∏ v: V, h (var v) = α v.
  Proof.
    exists (vevalhom a α).
    intro n.
    apply idpath.
  Defined.

  Definition iscontr_universalmap {a: algebra sigma} {V: hSet} (α: V → support a)
    : iscontr (∑ h: free_algebra sigma V ↦ a, ∏ v: V, h (var v) = α v).
  Proof.
    exists (universalmap α).
    intro h.
    induction h as [h hvar].
    apply subtypePairEquality'.
    - apply subtypePairEquality'.
      2: apply isapropishom.
      apply funextfun.
      unfold homot.
      apply term_ind.
      unfold term_ind_HP.
      intros.
      induction nm as [nm | var].
      * change (inl nm) with (namelift(V:=V) nm).
        change (build_term (namelift nm) v) with (op (free_algebra sigma V) nm v) at 1.
        rewrite (hom2axiom h).
        rewrite vevalstep.
        apply maponpaths.
        apply vector_extens.
        intros.
        do 2 rewrite el_vector_map.
        rewrite IH.
        apply idpath.
     * apply hvar.
   - apply impred_isaprop.
     intros.
     apply (support a).
  Defined.

End FreeAlgebras.
